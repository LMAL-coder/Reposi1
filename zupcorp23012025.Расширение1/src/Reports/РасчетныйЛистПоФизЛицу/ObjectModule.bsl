Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	ПараметрПериод = НастройкиОтчета.ПараметрыДанных.Элементы.Найти("Период");
	Если ПараметрПериод.Использование Тогда
		Период1 = ПараметрПериод.Значение;
	Иначе
		Период1 = Дата(2024,01,01,00,00,00);
	КонецЕсли; 

	ДатаНачала=Период1.ДатаНачала;
	ДатаОкончания=Период1.ДатаОкончания;
	ПараметрыПодключения = ЗГ_ПроцедурыИФункцииСервер.ПолучитьПараметрыПодключения("ЗУП ПРОФ 3.1");
	Если НЕ ЗначениеЗаполнено(ПараметрыПодключения.Сервер) Тогда
		ВызватьИсключение "Не найдены параметры подключения к базе «ЗУП ПРОФ»";
	КонецЕсли;
	
	Соединение = Новый HTTPСоединение(ПараметрыПодключения.Сервер,, ПараметрыПодключения.Пользователь, ПараметрыПодключения.Пароль);
	ЗапросHTTP = Новый HTTPЗапрос("/"+ПараметрыПодключения.ПубликацияНаВебСервере+"/hs/api/test");
	ОтветHTTP = Соединение.Получить(ЗапросHTTP);
	Если ОтветHTTP.КодСостояния <> 200 Тогда
		ВызватьИсключение "Произошла ошибка при подключении к базе «ЗУП ПРОФ» ";
	КонецЕсли;
	//Получаем из ЗУП стоимость отпусков Н_ 
	//ДатаНачала=Дата(2024,10,1);
	//ДатаОкончания=Дата(2024,10,31);
	//Подразделение=Справочники.ПодразделенияОрганизаций.НайтиПоНаименованию("Аптека Здоровый Город №12");
	
	ТаблицаОтпуска=Новый ТаблицаЗначений;
	//"Сотрудник,ДатаНачала,ДатаОкончания,КоличествоДней,Сумма,ЭтоКомпенсация,Должность,ИНН"
	ТаблицаОтпуска.Колонки.Добавить("ИНН", Новый ОписаниеТипов("Строка", ,,, Новый КвалификаторыСтроки(20)));
	ТаблицаОтпуска.Колонки.Добавить("КоличествоДней", Новый ОписаниеТипов("Число"));
	ТаблицаОтпуска.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	
	Запрос = Новый HTTPЗапрос(СтрШаблон("/"+ПараметрыПодключения.ПубликацияНаВебСервере+"/hs/api/oplataotpusk/%1",Формат(ДатаНачала, "ДФ=yyyyMMdd")));
	Ответ = Соединение.Получить(Запрос);
	Если Ответ.КодСостояния = 200 Тогда
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Данные1 = ПрочитатьJSON(Чтение);
		Для Каждого Стр Из Данные1 Цикл
			Нов = ТаблицаОтпуска.Добавить();
			//Нов.Период=Дата(СтрЗаменить(Лев(Стр.Период,10),"-",""));
			Нов.ИНН=Стр.ИНН;
			Нов.КоличествоДней=Стр.КоличествоДней;
			Нов.Сумма=Стр.Сумма;
		КонецЦикла
	КонецЕсли;
	ТаблицаОтпуска.Свернуть("ИНН","КоличествоДней,Сумма");
	//Получаем и ЗУП стоимость отпусков _К
	
	//Получаем из ЗУП стоимость больничных Н_
	ТаблицаБольничные=Новый ТаблицаЗначений;
	//"ФизическоеЛицо,ИНН,НомерЛН,ДатаНачала,ДатаОкончания,КоличествоДней,СуммаФинансируемаяФСС,СуммаФинансируемаяРаботодателем"
	ТаблицаБольничные.Колонки.Добавить("ИНН", Новый ОписаниеТипов("Строка", ,,, Новый КвалификаторыСтроки(20)));
	ТаблицаБольничные.Колонки.Добавить("КоличествоДней", Новый ОписаниеТипов("Число"));
	ТаблицаБольничные.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	Запрос = Новый HTTPЗапрос(СтрШаблон("/"+ПараметрыПодключения.ПубликацияНаВебСервере+"/hs/api/oplataboll/%1",Формат(ДатаНачала, "ДФ=yyyyMMdd")));
	Ответ = Соединение.Получить(Запрос);
	Если Ответ.КодСостояния = 200 Тогда
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		Данные2 = ПрочитатьJSON(Чтение);
		Для Каждого Стр Из Данные2 Цикл
			Нов = ТаблицаБольничные.Добавить();
			//Нов.Период=Дата(СтрЗаменить(Лев(Стр.Период,10),"-",""));
			Нов.ИНН=Стр.ИНН;
			Нов.КоличествоДней=Стр.КоличествоДней;
			Нов.Сумма=Стр.СуммаФинансируемаяРаботодателем;
		КонецЦикла
	КонецЕсли;
	ТаблицаБольничные.Свернуть("ИНН","КоличествоДней,Сумма");
	//Получаем и ЗУП стоимость больничных _К 
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Больничные", ТаблицаБольничные);
	Запрос.УстановитьПараметр("Отпуска", ТаблицаОтпуска);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Больничные.ИНН КАК ИНН,
		|	Больничные.КоличествоДней КАК КоличествоДней,
		|	Больничные.Сумма КАК Сумма
		|ПОМЕСТИТЬ Больничные
		|ИЗ
		|	&Больничные КАК Больничные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Отпуска.ИНН КАК ИНН,
		|	Отпуска.КоличествоДней КАК КоличествоДней,
		|	Отпуска.Сумма КАК Сумма
		|ПОМЕСТИТЬ Отпуска
		|ИЗ
		|	&Отпуска КАК Отпуска
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПоАптекам.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СУММА(ДанныеПоАптекам.ВремяДень) КАК ВремяДень,
		|	СУММА(ДанныеПоАптекам.ВремяНочь) КАК ВремяНочь,
		|	СУММА(ДанныеПоАптекам.ПраздникиВремяДень) КАК ПраздникиВремяДень,
		|	СУММА(ДанныеПоАптекам.ПраздникиВремяНочь) КАК ПраздникиВремяНочь,
		|	СУММА(ДанныеПоАптекам.ЛичнаяВыручкаНеДТМ) КАК ЛичнаяВыручкаНеДТМ,
		|	СУММА(ДанныеПоАптекам.БонусЗаЛичнуюВыручку) КАК БонусЗаЛичнуюВыручку,
		|	СУММА(ДанныеПоАптекам.ЛичнаяВыручкаДТМ) КАК ЛичнаяВыручкаДТМ,
		|	СУММА(ДанныеПоАптекам.БонусПоДМ) КАК БонусПоДМ,
		|	СУММА(ДанныеПоАптекам.ЛичнаяВыручкаОптика) КАК ЛичнаяВыручкаОптика,
		|	СУММА(ДанныеПоАптекам.БонусЗаОптику) КАК БонусЗаОптику,
		|	СУММА(ДанныеПоАптекам.СуммаПродажТоваровНаПродвижении) КАК СуммаПродажТоваровНаПродвижении,
		|	СУММА(ДанныеПоАптекам.СуммаПродажСрокиИЗМЗ) КАК СуммаПродажСрокиИЗМЗ
		|ПОМЕСТИТЬ ВТ_ДанныеПоАптекам
		|ИЗ
		|	РегистрСведений.ДанныеПоАптекам КАК ДанныеПоАптекам
		|ГДЕ
		|	ДанныеПоАптекам.ДатаС МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ДанныеПоАптекам.ДатаПо МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеПоАптекам.ФизическоеЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияЗППоАптекам.Период КАК Период,
		|	НачисленияЗППоАптекам.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НачисленияЗППоАптекам.ОсновнаяАптека КАК ОсновноеПодразделение,
		|	НачисленияЗППоАптекам.Должность КАК Должность,
		|	НачисленияЗППоАптекам.РазрядКатегория КАК РазрядКатегория,
		|	НачисленияЗППоАптекам.ТН КАК ТН,
		|	НачисленияЗППоАптекам.ИНН КАК ИНН,
		|	НачисленияЗППоАптекам.ВсегоПоРасчету КАК ВсегоПоРасчету,
		|	НачисленияЗППоАптекам.ИтогоДоГраниц КАК ИтогоДоГраниц,
		|	НачисленияЗППоАптекам.МинимумПоФакту КАК МинимумПоФакту,
		|	НачисленияЗППоАптекам.МаксимумПоФакту КАК МаксимумПоФакту,
		|	НачисленияЗППоАптекам.ВсегоПоРасчету - НачисленияЗППоАптекам.ИтогоДоГраниц - НачисленияЗППоАптекам.Доплата1 - НачисленияЗППоАптекам.Доплата2 КАК КоррекцияПремии1,
		|	НачисленияЗППоАптекам.Доплата2 КАК КоррекцияПремии2,
		|	НачисленияЗППоАптекам.ОтработаноВсего КАК ОтработаноВсего,
		|	НачисленияЗППоАптекам.ИтогоЗаОтработанноеВремя КАК ИтогоЗаОтработанноеВремя,
		|	НачисленияЗППоАптекам.Оклад0_166_Часы КАК Оклад0_166_Часы,
		|	НачисленияЗППоАптекам.Оклад0_166Сумма КАК Оклад0_166Сумма,
		|	НачисленияЗППоАптекам.Ночь_Часы КАК Ночь_Часы,
		|	НачисленияЗППоАптекам.Ночь_Сумма КАК Ночь_Сумма,
		|	НачисленияЗППоАптекам.ПраздникиДеньЧасы КАК ПраздникиДеньЧасы,
		|	НачисленияЗППоАптекам.ПраздникиДеньСумма КАК ПраздникиДеньСумма,
		|	НачисленияЗППоАптекам.ПраздникиНочьЧасы КАК ПраздникиНочьЧасы,
		|	НачисленияЗППоАптекам.ПраздникиНочьСумма КАК ПраздникиНочьСумма,
		|	НачисленияЗППоАптекам.ИтогоБонусов КАК ИтогоБонусов,
		|	ВТ_ДанныеПоАптекам.ЛичнаяВыручкаНеДТМ КАК ЛичнаяВыручкаНеДТМ,
		|	НачисленияЗППоАптекам.БонусНеДТМ КАК БонусНеДТМ,
		|	ВТ_ДанныеПоАптекам.ЛичнаяВыручкаДТМ КАК ЛичнаяВыручкаДТМ,
		|	НачисленияЗППоАптекам.БонусДТМ КАК БонусДТМ,
		|	ВТ_ДанныеПоАптекам.ЛичнаяВыручкаОптика КАК ЛичнаяВыручкаОптика,
		|	НачисленияЗППоАптекам.БонусЗаОптику КАК БонусЗаОптику,
		|	ВТ_ДанныеПоАптекам.СуммаПродажТоваровНаПродвижении КАК СуммаПродажТоваровНаПродвижении,
		|	НачисленияЗППоАптекам.БонусАкции КАК БонусАкции,
		|	ВТ_ДанныеПоАптекам.СуммаПродажСрокиИЗМЗ КАК СуммаПродажСрокиИЗМЗ,
		|	НачисленияЗППоАптекам.БонусЗМЗ КАК БонусЗМЗ,
		|	НачисленияЗППоАптекам.Доплата1 КАК Доплата1,
		|	ВЫБОР
		|		КОГДА ВТ_ДанныеПоАптекам.ЛичнаяВыручкаНеДТМ <> 0
		|			ТОГДА ОКР(НачисленияЗППоАптекам.БонусНеДТМ / ВТ_ДанныеПоАптекам.ЛичнаяВыручкаНеДТМ * 100, 2)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПоказательНеДТМ,
		|	ВЫБОР
		|		КОГДА ВТ_ДанныеПоАптекам.ЛичнаяВыручкаДТМ <> 0
		|			ТОГДА ОКР(НачисленияЗППоАптекам.БонусДТМ / ВТ_ДанныеПоАптекам.ЛичнаяВыручкаДТМ * 100, 2)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПоказательДТМ,
		|	ВЫБОР
		|		КОГДА ВТ_ДанныеПоАптекам.ЛичнаяВыручкаОптика <> 0
		|			ТОГДА ОКР(НачисленияЗППоАптекам.БонусЗаОптику / ВТ_ДанныеПоАптекам.ЛичнаяВыручкаОптика * 100, 2)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПоказательОптика,
		|	ВЫБОР
		|		КОГДА НачисленияЗППоАптекам.Оклад0_166_Часы <> 0
		|			ТОГДА ОКР(НачисленияЗППоАптекам.Оклад0_166Сумма / НачисленияЗППоАптекам.Оклад0_166_Часы, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК День1,
		|	ВЫБОР
		|		КОГДА НачисленияЗППоАптекам.Ночь_Часы <> 0
		|			ТОГДА ОКР(НачисленияЗППоАптекам.Ночь_Сумма / НачисленияЗППоАптекам.Ночь_Часы, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Ночь1,
		|	ВЫБОР
		|		КОГДА НачисленияЗППоАптекам.ПраздникиДеньЧасы <> 0
		|			ТОГДА ОКР(НачисленияЗППоАптекам.ПраздникиДеньСумма / НачисленияЗППоАптекам.ПраздникиДеньЧасы)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК День2,
		|	ВЫБОР
		|		КОГДА НачисленияЗППоАптекам.ПраздникиНочьЧасы <> 0
		|			ТОГДА ОКР(НачисленияЗППоАптекам.ПраздникиНочьСумма / НачисленияЗППоАптекам.ПраздникиНочьЧасы, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Ночь2,
		|	Больничные.Сумма КАК БольничныеСумма,
		|	Отпуска.Сумма КАК ОтпускаСумма,
		|	Больничные.КоличествоДней КАК БольничныеДни,
		|	Отпуска.КоличествоДней КАК ОтпускаДни,
		|	НачисленияЗППоАптекам.ПремияЗаКатегориюСтоимость КАК ПремияЗаКатегориюСтоимость,
		|	НачисленияЗППоАптекам.ДоплатаЗаКатегорию КАК ДоплатаЗаКатегорию,
		|	НачисленияЗППоАптекам.ПремияНочнаяСтоимость КАК ПремияНочнаяСтоимость,
		|	НачисленияЗППоАптекам.ПремияНочнаяСумма КАК ПремияНочнаяСумма,
		|	НачисленияЗППоАптекам.КолПринятыхЗвонков КАК КолПринятыхЗвонков,
		|	ВЫБОР
		|		КОГДА НачисленияЗППоАптекам.КолПринятыхЗвонков <> 0
		|			ТОГДА ОКР(НачисленияЗППоАптекам.БонусЗвонки / НачисленияЗППоАптекам.КолПринятыхЗвонков, 2)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтоимостьЗвонка,
		|	НачисленияЗППоАптекам.БонусЗвонки КАК БонусЗвонки,
		|	НачисленияЗППоАптекам.КолСобранныхУпаковок КАК КолСобранныхУпаковок,
		|	ВЫБОР
		|		КОГДА НачисленияЗППоАптекам.КолСобранныхУпаковок <> 0
		|			ТОГДА ОКР(НачисленияЗППоАптекам.БонусСтрокиЗаказов / НачисленияЗППоАптекам.КолСобранныхУпаковок, 2)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтоимостьСобраннойУпаковки,
		|	НачисленияЗППоАптекам.БонусСтрокиЗаказов КАК БонусСтрокиЗаказов
		|ПОМЕСТИТЬ ВТ_ЗП
		|ИЗ
		|	РегистрСведений.НачисленияЗППоАптекам КАК НачисленияЗППоАптекам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДанныеПоАптекам КАК ВТ_ДанныеПоАптекам
		|		ПО НачисленияЗППоАптекам.ФизическоеЛицо = ВТ_ДанныеПоАптекам.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ Больничные КАК Больничные
		|		ПО НачисленияЗППоАптекам.ИНН = Больничные.ИНН
		|		ЛЕВОЕ СОЕДИНЕНИЕ Отпуска КАК Отпуска
		|		ПО НачисленияЗППоАптекам.ИНН = Отпуска.ИНН
		|ГДЕ
		|	НачисленияЗППоАптекам.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо КАК ФизическоеЛицо,
		|	""01 Отработано день, часы"" КАК Вид,
		|	ВТ_ЗП.Оклад0_166_Часы КАК Показатель,
		|	ВТ_ЗП.День1 КАК Тариф,
		|	ВТ_ЗП.Оклад0_166Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ_Объединение
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""02 Премия за категорию (почасовая)"",
		|	ВТ_ЗП.Оклад0_166_Часы,
		|	ВТ_ЗП.ПремияЗаКатегориюСтоимость,
		|	ВТ_ЗП.ДоплатаЗаКатегорию
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""03 Отработано ночь, часы"",
		|	ВТ_ЗП.Ночь_Часы,
		|	ВТ_ЗП.Ночь1,
		|	ВТ_ЗП.Ночь_Сумма
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""04 Премия ночная (почасовая)"",
		|	ВТ_ЗП.Ночь_Часы,
		|	ВТ_ЗП.ПремияНочнаяСтоимость,
		|	ВТ_ЗП.ПремияНочнаяСумма
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""05 Праздники день, часы"",
		|	ВТ_ЗП.ПраздникиДеньЧасы,
		|	ВТ_ЗП.День2,
		|	ВТ_ЗП.ПраздникиДеньСумма
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""06 Праздники ночь, часы"",
		|	ВТ_ЗП.ПраздникиНочьЧасы,
		|	ВТ_ЗП.Ночь2,
		|	ВТ_ЗП.ПраздникиНочьСумма
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""07 Премия не ДТМ и не оптики"",
		|	ВТ_ЗП.ЛичнаяВыручкаНеДТМ,
		|	ВТ_ЗП.ПоказательНеДТМ,
		|	ВТ_ЗП.БонусНеДТМ
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""08 Премия ДТМ"",
		|	ВТ_ЗП.ЛичнаяВыручкаДТМ,
		|	ВТ_ЗП.ПоказательДТМ,
		|	ВТ_ЗП.БонусДТМ
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""09 Премия оптики"",
		|	ВТ_ЗП.ЛичнаяВыручкаОптика,
		|	ВТ_ЗП.ПоказательОптика,
		|	ВТ_ЗП.БонусЗаОптику
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""10 Премия за маркетинг"",
		|	ВТ_ЗП.СуммаПродажТоваровНаПродвижении,
		|	0,
		|	ВТ_ЗП.БонусАкции
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""11 Премия сроки и ЗМЗ"",
		|	ВТ_ЗП.СуммаПродажСрокиИЗМЗ,
		|	0,
		|	ВТ_ЗП.БонусЗМЗ
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""12 Премия за собранные упаковки по заказам, принятых кон. ИА"",
		|	ВТ_ЗП.КолСобранныхУпаковок,
		|	ВТ_ЗП.СтоимостьСобраннойУпаковки,
		|	ВТ_ЗП.БонусСтрокиЗаказов
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""13 Премия принятые звонки консультантами ИА"",
		|	ВТ_ЗП.КолПринятыхЗвонков,
		|	ВТ_ЗП.СтоимостьЗвонка,
		|	ВТ_ЗП.БонусЗвонки
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""14 Корректировка премии1 (min/max)"",
		|	0,
		|	0,
		|	ВТ_ЗП.КоррекцияПремии1
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""15 Корректировка премии2"",
		|	0,
		|	0,
		|	ВТ_ЗП.КоррекцияПремии2
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""16 Больничные, дни"",
		|	ВТ_ЗП.БольничныеДни,
		|	0,
		|	ВТ_ЗП.БольничныеСумма
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""17 Отпуск, дни"",
		|	ВТ_ЗП.ОтпускаДни,
		|	0,
		|	ВТ_ЗП.ОтпускаСумма
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо,
		|	""18 Доплата за командировки"",
		|	0,
		|	0,
		|	ВТ_ЗП.Доплата1
		|ИЗ
		|	ВТ_ЗП КАК ВТ_ЗП
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Объединение.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВТ_Объединение.Вид КАК Вид,
		|	СУММА(ВТ_Объединение.Показатель) КАК Показатель,
		|	МАКСИМУМ(ВТ_Объединение.Тариф) КАК Тариф,
		|	СУММА(ВТ_Объединение.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТ_ПоказателиИтог
		|ИЗ
		|	ВТ_Объединение КАК ВТ_Объединение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Объединение.ФизическоеЛицо,
		|	ВТ_Объединение.Вид
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ЗП.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВТ_ЗП.ОсновноеПодразделение КАК ОсновноеПодразделение,
		|	ВТ_ЗП.Должность КАК Должность,
		|	ВТ_ЗП.РазрядКатегория КАК РазрядКатегория,
		|	ВТ_ЗП.ВсегоПоРасчету КАК ВсегоПоРасчету,
		|	ВТ_ЗП.ИтогоДоГраниц КАК ИтогоДоГраниц,
		|	ВТ_ПоказателиИтог.Вид КАК Вид,
		|	ВТ_ПоказателиИтог.Показатель КАК Показатель,
		|	ВТ_ПоказателиИтог.Тариф КАК Тариф,
		|	ВТ_ПоказателиИтог.Сумма КАК Сумма,
		|	ВТ_ЗП.МинимумПоФакту КАК МинимумПоФакту,
		|	ВТ_ЗП.МаксимумПоФакту КАК МаксимумПоФакту,
		|	ВТ_ЗП.ТН КАК ТН,
		|	ВТ_ЗП.ИНН КАК ИНН
		|ИЗ
		|	ВТ_ПоказателиИтог КАК ВТ_ПоказателиИтог
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗП КАК ВТ_ЗП
		|		ПО ВТ_ПоказателиИтог.ФизическоеЛицо = ВТ_ЗП.ФизическоеЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_ПоказателиИтог.ФизическоеЛицо,
		|	ВТ_ПоказателиИтог.Вид";
	
	
	
	ТаблицаИтог = Запрос.Выполнить().Выгрузить();
	
	
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();	
	ДокументРезультат.Очистить();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ВнешнийИД =  Новый Структура;
	ВнешнийИД.Вставить("ТаблицаИтог", ТаблицаИтог);
		
	// Создадим и инициализируем процессор компоновки.
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешнийИД , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	// Обозначим начало вывода.
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);

КонецПроцедуры