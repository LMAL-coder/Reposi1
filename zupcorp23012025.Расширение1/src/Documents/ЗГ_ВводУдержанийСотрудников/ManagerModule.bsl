
Функция ПолучитьРезультатЗапросаФизическихЛицДляЗаполнения(Подразделение, ПериодРегистрации, Должность = Неопределено) Экспорт 
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	Если ЗначениеЗаполнено(Подразделение) Тогда
		ПараметрыПолученияСотрудников.Подразделение 	= Подразделение;
	КонецЕсли;
	ПараметрыПолученияСотрудников.НачалоПериода		= НачалоМесяца(ПериодРегистрации);
	ПараметрыПолученияСотрудников.ОкончаниеПериода  = КонецМесяца(ПериодРегистрации);
	ПараметрыПолученияСотрудников.КадровыеДанные = "Должность, Подразделение, ВидЗанятости";
	
	Отборы = Новый Массив;
	СписокВидовЗанятостей = Новый Массив;
	СписокВидовЗанятостей.Добавить(Перечисления.ВидыЗанятости.ОсновноеМестоРаботы);
	СписокВидовЗанятостей.Добавить(Перечисления.ВидыЗанятости.Совместительство);
	Отборы.Добавить(Новый Структура ("ЛевоеЗначение, ВидСравнения, ПравоеЗначение, ОтносительныйПуть", "ВидЗанятости", "В", СписокВидовЗанятостей, Истина));
	ПараметрыПолученияСотрудников.Отборы = Отборы;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудников, "ВТВсеСотрудникиОрганизации");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 		
	    "ВЫБРАТЬ
	    |	ЗГ_УдержанияСотрудниковОрганизацииОбороты.ФизическоеЛицо КАК ФизическоеЛицо
	    |ПОМЕСТИТЬ ВТ_ФизЛицаИсключения
	    |ИЗ
	    |	РегистрНакопления.ЗГ_УдержанияСотрудниковОрганизации.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ЗГ_УдержанияСотрудниковОрганизацииОбороты
	    |ГДЕ
	    |	ЗГ_УдержанияСотрудниковОрганизацииОбороты.СуммаПриход <> 0
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ
	    |	Сотрудники.Период КАК Период,
	    |	Сотрудники.Сотрудник КАК Сотрудник,
	    |	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	    |	Сотрудники.Подразделение КАК Подразделение,
	    |	Сотрудники.Должность КАК Должность,
	    |	Сотрудники.ВидЗанятости КАК ВидЗанятости
	    |ПОМЕСТИТЬ ВТ_РаботающиеСотрудники
	    |ИЗ
	    |	ВТВсеСотрудникиОрганизации КАК Сотрудники
	    |ГДЕ
	    |	Сотрудники.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
	    |	И ВЫБОР
	    |			КОГДА &ОтбиратьДолжность
	    |				ТОГДА Сотрудники.Должность = &Должность
	    |			ИНАЧЕ ИСТИНА
	    |		КОНЕЦ
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	    |	КадроваяИсторияСотрудниковИнтервальный.ФизическоеЛицо КАК ФизическоеЛицо,
	    |	КадроваяИсторияСотрудниковИнтервальный.Подразделение КАК Подразделение,
	    |	КадроваяИсторияСотрудниковИнтервальный.Должность КАК Должность,
	    |	ВидыЗанятостиСотрудниковСрезПоследних.ВидЗанятости КАК ВидЗанятости
	    |ПОМЕСТИТЬ ВТ_УволенныеСотрудники
	    |ИЗ
	    |	РегистрСведений.КадроваяИсторияСотрудниковИнтервальный КАК КадроваяИсторияСотрудниковИнтервальный
	    |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВидыЗанятостиСотрудников.СрезПоследних(&КонецПериода, ) КАК ВидыЗанятостиСотрудниковСрезПоследних
	    |		ПО КадроваяИсторияСотрудниковИнтервальный.Сотрудник = ВидыЗанятостиСотрудниковСрезПоследних.Сотрудник
	    |			И КадроваяИсторияСотрудниковИнтервальный.ГоловнаяОрганизация = ВидыЗанятостиСотрудниковСрезПоследних.ГоловнаяОрганизация
	    |			И КадроваяИсторияСотрудниковИнтервальный.ФизическоеЛицо = ВидыЗанятостиСотрудниковСрезПоследних.ФизическоеЛицо
	    |ГДЕ
	    |	НЕ КадроваяИсторияСотрудниковИнтервальный.ФизическоеЛицо В
	    |				(ВЫБРАТЬ
	    |					ВТ_РаботающиеСотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	    |				ИЗ
	    |					ВТ_РаботающиеСотрудники КАК ВТ_РаботающиеСотрудники)
	    |	И ВЫБОР
	    |			КОГДА &ОтбиратьПодразделение
	    |				ТОГДА КадроваяИсторияСотрудниковИнтервальный.Подразделение = &Подразделение
	    |			ИНАЧЕ ИСТИНА
	    |		КОНЕЦ
	    |	И КадроваяИсторияСотрудниковИнтервальный.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	    |	И НАЧАЛОПЕРИОДА(КадроваяИсторияСотрудниковИнтервальный.ДатаНачала, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
	    |	И ВЫБОР
	    |			КОГДА &ОтбиратьДолжность
	    |				ТОГДА КадроваяИсторияСотрудниковИнтервальный.Должность = &Должность
	    |			ИНАЧЕ ИСТИНА
	    |		КОНЕЦ
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	    |	ВТ_РаботающиеСотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	    |	ВТ_РаботающиеСотрудники.Подразделение КАК Подразделение,
	    |	ВТ_РаботающиеСотрудники.Должность КАК Должность,
	    |	ЛОЖЬ КАК Уволен
	    |ИЗ
	    |	ВТ_РаботающиеСотрудники КАК ВТ_РаботающиеСотрудники
	    |ГДЕ
	    |	НЕ ВТ_РаботающиеСотрудники.ФизическоеЛицо В
	    |				(ВЫБРАТЬ
	    |					ВТ_ФизЛицаИсключения.ФизическоеЛицо КАК ФизическоеЛицо
	    |				ИЗ
	    |					ВТ_ФизЛицаИсключения КАК ВТ_ФизЛицаИсключения)
	    |
	    |ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ РАЗЛИЧНЫЕ
	    |	ВТ_УволенныеСотрудники.ФизическоеЛицо,
	    |	ВТ_УволенныеСотрудники.Подразделение,
	    |	ВТ_УволенныеСотрудники.Должность,
	    |	ИСТИНА
	    |ИЗ
	    |	ВТ_УволенныеСотрудники КАК ВТ_УволенныеСотрудники
	    |ГДЕ
	    |	ВТ_УволенныеСотрудники.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	    |	И НЕ ВТ_УволенныеСотрудники.ФизическоеЛицо В
	    |				(ВЫБРАТЬ
	    |					ВТ_ФизЛицаИсключения.ФизическоеЛицо КАК ФизическоеЛицо
	    |				ИЗ
	    |					ВТ_ФизЛицаИсключения КАК ВТ_ФизЛицаИсключения)";
		
	Запрос.УстановитьПараметр("ОтбиратьПодразделение", ЗначениеЗаполнено(Подразделение));
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ОтбиратьДолжность", ЗначениеЗаполнено(Должность));
	Запрос.УстановитьПараметр("Должность", Должность);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПериодРегистрации));
	
	Результат = Запрос.Выполнить(); 
	
	Возврат Результат;
	
КонецФункции